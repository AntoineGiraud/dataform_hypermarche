config {
  type: "table",
  description: "Dimension des villes : qq villes où nos clients sont localisés",
  columns: {
    id_ville: "ID recomposé des villes (clé de substitution)",
  },
  assertions: {
    uniqueKey: ["id_ville"],
    nonNull:["id_ville"]
  },
  dependOnDependencyAssertions: true,
  dependencies: [
    "stg_commande_assertions_uniqueKey_0",
    {name: "stg_commande", includeDependentAssertions: true}
  ]
}

post_operations {
    ${ refresh_tableau('dim_ville') }
}

with villes as (
    select distinct
        ville_nom as nom,
        ville_region as region,
        ville_pays as pays,
        ville_zone as zone
    from ${ ref('stg_commande') }
)

 select
    row_number() over (order by zone, pays, region, nom) as id_ville,
    v.*,
    z.responsable
from villes as v
    left join ${ ref('stg_zone_has_responsable') } as z using (zone)
order by id_ville
